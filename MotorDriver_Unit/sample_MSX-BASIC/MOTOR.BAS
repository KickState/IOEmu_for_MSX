100 '
110 ' TITLE: SAMPLE for Motor-Driver Unit
120 ' FILE: motor.bas
130 '
140 SCREEN0:WIDTH40
150 '
160 'Init
170 DEFINT A-Z:TF=0:TC=0:RV=0:CL=3:S1=0:RE=0:VA=0:SD=0
180 OUT &H1F,1
190 '_COMINI("0:8N1NNNNN",9600)
200 'OPEN "COM:" FOR OUTPUT AS #1
210 '
220 'Message
230 LOCATE 0,0:PRINT "Motor-Driver Unit for IOEmu"
240 LOCATE 0,CL+0:PRINT "> Please Power on Motor-Driver Unit.";SPC(20)
250 LOCATE 0,CL+1:PRINT "> Press any key to start.";SPC(20)
260 IF INKEY$="" THEN 260
270 LOCATE 0,CL+0:PRINT "> Let's try Motor-Driver Unit!";SPC(20)
280 GOSUB 1320
290 MD=&H3F:GOSUB 940:MD=&HFF:GOSUB940
300 '
310 'Enable interrupt
320 ON STOP GOSUB 1030:STOP ON
330 ON INTERVAL=6 GOSUB 990 'Sync:0.1s
340 INTERVAL ON
350 '
360 'Main loop
370 IF TF=0 THEN 370
380 TF=0:K$=INKEY$
390 IF K$="" THEN GOSUB 580:GOTO 370
400 IF K$=CHR$(&H1C) THEN GOSUB 610:GOTO 370
410 IF K$=CHR$(&H1D) THEN GOSUB 640:GOTO 370
420 IF K$=CHR$(&H1E) THEN GOSUB 670:GOTO 370
430 IF K$=CHR$(&H1F) THEN GOSUB 690:GOTO 370
440 IF K$="s" OR K$="S" THEN GOSUB 710:GOTO 370
450 IF K$="a" OR K$="A" THEN GOSUB 750:GOTO 370
460 IF K$="d" OR K$="D" THEN GOSUB 730:GOTO 370
470 IF K$="w" OR K$="W" THEN GOSUB 770:GOTO 370
480 IF K$="x" OR K$="X" THEN GOSUB 790:GOTO 370
490 IF K$="q" OR K$="Q" THEN GOSUB 810:GOTO 370
500 IF K$="r" OR K$="R" THEN GOSUB 830:GOTO 370
510 IF K$="p" OR K$="P" THEN GOSUB 850:GOTO 370
520 IF K$="e" OR K$="E" THEN GOSUB 870:GOTO 370
530 IF K$="g" OR K$="G" THEN GOSUB 890:GOTO 370
540 IF K$="1" THEN GOSUB 910:GOTO 370
550 GOTO 370
560 '
570 'Commands
580 'NOP
590 MD=&H0:GOSUB 940:RETURN
600 'RIGHT
610 IF RV=0 THEN MD=&H5 ELSE MD=&H8
620 GOSUB 940:RETURN
630 'LEFT
640 IF RV=0 THEN MD=&H6 ELSE MD=&H7
650 GOSUB 940:RETURN
660 'FORWARD
670 MD=&H2:RV=0:GOSUB 940:RETURN
680 'REVERSE
690 MD=&H3:RV=1:GOSUB 940:RETURN
700 'STOP
710 MD=&H1:GOSUB 940:RETURN
720 'RTURN
730 MD=&H9:GOSUB 940:RETURN
740 'LTURN
750 MD=&HA:GOSUB 940:RETURN
760 'LIFTUP
770 MD=&HC:GOSUB 940:RETURN
780 'LIFTDWN
790 MD=&HD:GOSUB 940:RETURN
800 'LIFTSTP
810 MD=&HB:GOSUB 940:RETURN
820 'REC
830 MD=&H10:GOSUB 940:RETURN
840 'PLAY
850 MD=&H11:GOSUB 940:RETURN
860 'RP-STOP
870 MD=&H12:GOSUB 940:RETURN
880 'GETSNS1
890 MD=&H14:GOSUB 940:GOSUB 1060:RETURN
900 'ENSNS1
910 MD=&H13:GOSUB 940:GOSUB 1140:RETURN
920 '
930 'Send command
940 OUT &H10,MD
950 'PRINT #1,CHR$(MD);
960 RETURN
970 '
980 'Timer Interrupt
990 TF=1':TC=TC+1
1000 RETURN
1010 '
1020 'CTRL+STOP
1030 MD=&H1:GOSUB 940:MD=&HB:GOSUB 940:CLS:END
1040 '
1050 'Get Sensor1 value
1060 IF NOT S1 THEN RETURN
1070 GOSUB 1210:SD=VA*256
1080 GOSUB 1210:SD=SD+VA:V!=SD*.017:'VA=V!
1090 LOCATE 0,CL+1:PRINT ">  Sensor1 Value: "+STR$(V!)+"cm";SPC(20)
1100 GOSUB 1280
1110 RETURN
1120 '
1130 'Toggle Sensor1 flag
1140 GOSUB 1210:IF VA=&H55 THEN S1=-1 ELSE S1=0
1150 IF S1 THEN LOCATE 0,CL+0:PRINT "> Enable Sensor1.";SPC(20)
1160 IF NOT S1 THEN LOCATE 0,CL+0:PRINT "> Disable Sensor1.";SPC(20)
1170 GOSUB 1280
1180 RETURN
1190 '
1200 'Get rx-data
1210 TIME=0
1220 IF TIME>60 THEN VA=0:RETURN
1230 RE=INP(&H11):IF (RE AND &H1)<>1 THEN 1220
1240 VA=INP(&H10)
1250 RETURN
1260 '
1270 'Flash Rx-FIFO
1280 RE=INP(&H11):IF (RE AND &H1)=1 THEN VA=INP(&H10):GOTO 1280
1290 RETURN
1300 '
1310 'List of Command keys
1320 LOCATE 0,CL+ 1:PRINT "> List of command keys: ";SPC(20)
1330 LOCATE 0,CL+ 4:PRINT "# Cursor keys ---  Driving direction"
1340 PRINT "# [s|S] key   ---  Stop crawler motor"
1350 PRINT "# [a|A] key   ---  Turn left"
1360 PRINT "# [d|D] key   ---  Turn right"
1370 PRINT "# [w|W] key   ---  Lift uo"
1380 PRINT "# [x|X] key   ---  Lift down"
1390 PRINT "# [q|Q] key   ---  Stop lift"
1400 PRINT "# [r|R] key   ---  REC"
1410 PRINT "# [p|P] key   ---  PLAY"
1420 PRINT "# [e|E] key   ---  Stop Rec/Play"
1430 PRINT "# [g|G] key   ---  Get SensorX val"
1440 PRINT "# [1]   key   ---  Sensor1 On/Off toggle"
1450 RETURN
