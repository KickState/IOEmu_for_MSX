100 '
110 ' TITLE: SAMPLE for Motor-Driver Unit for MSX0 Stack/Card
120 ' FILE: mtr_msx0.bas
130 '
140 SCREEN0:WIDTH40
150 '
160 'Init
170 DEFINT A-Z:TF=0:TC=0:RV=0:CL=3:S1=0:RE=0:VA=0:SD=0
180 _COMINI("0:8N1NNNNN",9600)
190 OPEN "COM:" AS #1
200 '
210 'Message
220 LOCATE 0,0:PRINT "Motor-Driver Unit for IOEmu"
230 LOCATE 0,CL+0:PRINT "> Please Power on Motor-Driver Unit.";SPC(20)
240 LOCATE 0,CL+1:PRINT "> Press any key to start.";SPC(20)
250 IF INKEY$="" THEN 250
260 LOCATE 0,CL+0:PRINT "> Let's try Motor-Driver Unit!";SPC(20)
270 GOSUB 1300
280 MD=&H3F:GOSUB 930:MD=&HFF:GOSUB930
290 '
300 'Enable interrupt
310 ON STOP GOSUB 1010:STOP ON
320 ON INTERVAL=6 GOSUB 970 'Sync:0.1s
330 INTERVAL ON
340 '
350 'Main loop
360 IF TF=0 THEN 360
370 TF=0:K$=INKEY$
380 IF K$="" THEN GOSUB 570:GOTO 360
390 IF K$=CHR$(&H1C) THEN GOSUB 600:GOTO 360
400 IF K$=CHR$(&H1D) THEN GOSUB 630:GOTO 360
410 IF K$=CHR$(&H1E) THEN GOSUB 660:GOTO 360
420 IF K$=CHR$(&H1F) THEN GOSUB 680:GOTO 360
430 IF K$="s" OR K$="S" THEN GOSUB 700:GOTO 360
440 IF K$="a" OR K$="A" THEN GOSUB 740:GOTO 360
450 IF K$="d" OR K$="D" THEN GOSUB 720:GOTO 360
460 IF K$="w" OR K$="W" THEN GOSUB 760:GOTO 360
470 IF K$="x" OR K$="X" THEN GOSUB 780:GOTO 360
480 IF K$="q" OR K$="Q" THEN GOSUB 800:GOTO 360
490 IF K$="r" OR K$="R" THEN GOSUB 820:GOTO 360
500 IF K$="p" OR K$="P" THEN GOSUB 840:GOTO 360
510 IF K$="e" OR K$="E" THEN GOSUB 860:GOTO 360
520 IF K$="g" OR K$="G" THEN GOSUB 880:GOTO 360
530 IF K$="1" THEN GOSUB 900:GOTO 360
540 GOTO 360
550 '
560 'Commands
570 'NOP
580 MD=&H0:GOSUB 930:RETURN
590 'RIGHT
600 IF RV=0 THEN MD=&H5 ELSE MD=&H8
610 GOSUB 930:RETURN
620 'LEFT
630 IF RV=0 THEN MD=&H6 ELSE MD=&H7
640 GOSUB 930:RETURN
650 'FORWARD
660 MD=&H2:RV=0:GOSUB 930:RETURN
670 'REVERSE
680 MD=&H3:RV=1:GOSUB 930:RETURN
690 'STOP
700 MD=&H1:GOSUB 930:RETURN
710 'RTURN
720 MD=&H9:GOSUB 930:RETURN
730 'LTURN
740 MD=&HA:GOSUB 930:RETURN
750 'LIFTUP
760 MD=&HC:GOSUB 930:RETURN
770 'LIFTDWN
780 MD=&HD:GOSUB 930:RETURN
790 'LIFTSTP
800 MD=&HB:GOSUB 930:RETURN
810 'REC
820 MD=&H10:GOSUB 930:RETURN
830 'PLAY
840 MD=&H11:GOSUB 930:RETURN
850 'RP-STOP
860 MD=&H12:GOSUB 930:RETURN
870 'GETSNS1
880 MD=&H14:GOSUB 930:GOSUB 1040:RETURN
890 'ENSNS1
900 MD=&H13:GOSUB 930:GOSUB 1120:RETURN
910 '
920 'Send command
930 PRINT #1,CHR$(MD);
940 RETURN
950 '
960 'Timer Interrupt
970 TF=1':TC=TC+1
980 RETURN
990 '
1000 'CTRL+STOP
1010 MD=&H1:GOSUB 930:MD=&HB:GOSUB 930:CLS:END
1020 '
1030 'Get Sensor1 value
1040 IF NOT S1 THEN RETURN
1050 GOSUB 1190:SD=VA*256
1060 GOSUB 1190:SD=SD+VA:V!=SD*.017:'VA=V!
1070 LOCATE 0,CL+1:PRINT ">  Sensor1 Value: "+STR$(V!)+"cm";SPC(20)
1080 GOSUB 1260
1090 RETURN
1100 '
1110 'Toggle Sensor1 flag
1120 GOSUB 1190:IF VA=&H55 THEN S1=-1 ELSE S1=0
1130 IF S1 THEN LOCATE 0,CL+0:PRINT "> Enable Sensor1.";SPC(20)
1140 IF NOT S1 THEN LOCATE 0,CL+0:PRINT "> Disable Sensor1.";SPC(20)
1150 GOSUB 1260
1160 RETURN
1170 '
1180 'Get rx-data
1190 TIME=0
1200 IF TIME>60 THEN VA=0:RETURN
1210 IF LOC(1)=0 THEN 1200
1220 VA=ASC(INPUT$(1,#1))
1230 RETURN
1240 '
1250 'Flash Rx-Buffer
1260 IF LOC(1)<>0 THEN VA=ASC(INPUT$(1,#1)):GOTO 1260
1270 RETURN
1280 '
1290 'List of Command keys
1300 LOCATE 0,CL+ 1:PRINT "> List of command keys: ";SPC(20)
1310 LOCATE 0,CL+ 4:PRINT "# Cursor keys ---  Driving direction"
1320 PRINT "# [s|S] key   ---  Stop crawler motor"
1330 PRINT "# [a|A] key   ---  Turn left"
1340 PRINT "# [d|D] key   ---  Turn right"
1350 PRINT "# [w|W] key   ---  Lift uo"
1360 PRINT "# [x|X] key   ---  Lift down"
1370 PRINT "# [q|Q] key   ---  Stop lift"
1380 PRINT "# [r|R] key   ---  REC"
1390 PRINT "# [p|P] key   ---  PLAY"
1400 PRINT "# [e|E] key   ---  Stop Rec/Play"
1410 PRINT "# [g|G] key   ---  Get SensorX val"
1420 PRINT "# [1]   key   ---  Sensor1 On/Off toggle"
1430 RETURN
